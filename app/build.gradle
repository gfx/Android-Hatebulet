apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'android-power-assert'
apply plugin: 'com.neenbedankt.android-apt'
//apply plugin: 'galette'

apply from: 'https://raw.githubusercontent.com/gfx/gradle-android-utils/master/build.gradle'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots" // for dagger 2
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    defaultConfig {
        applicationId "com.github.gfx.helium"
        minSdkVersion 16
        targetSdkVersion 22

        def version = readVersion(rootProject.file("VERSION"))
        versionCode version.toVersionCode()
        versionName version.toVersionName()
    }

    signingConfigs {
        release {
            if (project.ext.has('ReleaseStoreFile')) {
                storeFile file(project.ext.ReleaseStoreFile.replaceAll("~", System.getenv("HOME")))
                storePassword project.ext.ReleaseStorePassword
                keyAlias project.ext.ReleaseKeyAlias
                keyPassword project.ext.ReleaseKeyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (signingConfigs.release.storeFile != null && signingConfigs.release.storeFile.exists()) {
                signingConfig signingConfigs.release
            } else {
                logger.info("keystore ${signingConfigs.release.storeFile} does not exist. Release signing is ignored.")
            }

           buildConfigField("String", "GA_TRACKING_ID", "\"${project.ext.has('GA_TRACKING_ID_RELEASE') ? project.ext.GA_TRACKING_ID_RELEASE : ''}\"")
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField("String", "GA_TRACKING_ID", "\"${project.ext.has('GA_TRACKING_ID_DEBUG') ? project.ext.GA_TRACKING_ID_DEBUG : ''}\"")
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:cardview-v7:22.0.0'
    compile 'com.android.support:support-annotations:22.0.0'
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'io.reactivex:rxjava:1.0.8'
    compile('com.squareup.retrofit:converter-simplexml:1.9.0') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile 'com.etsy.android.grid:library:1.0.5'
    compile 'net.danlew:android.joda:2.7.1'
    compile 'com.github.bumptech.glide:glide:3.5.2'

    compile 'com.google.dagger:dagger:2.0-SNAPSHOT'
    apt 'com.google.dagger:dagger-compiler:2.0-SNAPSHOT'
    provided 'javax.annotation:jsr250-api:1.0'

    compile 'com.facebook.stetho:stetho:1.0.1'
    compile 'com.facebook.stetho:stetho-okhttp:1.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.google.guava:guava:18.0'
}
